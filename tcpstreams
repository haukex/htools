#!/usr/bin/env perl
use warnings;
use strict;
use Getopt::Std 'getopts';
use Pod::Usage 'pod2usage';
use IPC::System::Simple qw/systemx capturex/;

# For AUTHOR, COPYRIGHT, AND LICENSE see the bottom of this file

=head1 SYNOPSIS

 tcpstreams [OPTIONS] [INFILES]
 OPTIONS:
   -a       - ASCII output (default is binary)
   -l / -r  - Only output left / right node (not with -a)
   -s N     - Show only stream number N
   -o FILE  - Output file (default is STDOUT)
 INFILES defaults to *.pcap*

=cut

sub HELP_MESSAGE { pod2usage(-output=>shift) }
sub VERSION_MESSAGE { say {shift} q$tcpstreams v0.01$ }
$Getopt::Std::STANDARD_HELP_VERSION = 1;
getopts('alrs:o:', \my %opts) or pod2usage;

my $ASCOUT = !!$opts{a};
pod2usage("-l/-r don't work with -a") if ($opts{l} || $opts{r}) && $opts{a};
my $SHOWONLY = $opts{l} ? ($opts{r} ? '' : 'left') : ($opts{r} ? 'right' : '');
my $STREAM = $opts{s};
pod2usage("bad stream number") if length $STREAM && $STREAM!~/\A\d+\z/;
my $OUTFILE = $opts{o};
my @FILES = @ARGV ? @ARGV : sort <*.pcap*>;
pod2usage("No files") unless @FILES;

if (defined $OUTFILE) {
	open my $fh, '>', $OUTFILE
		or die "Couldn't open $OUTFILE for write: $!";
	select $fh;
}
binmode select unless $ASCOUT;
for my $file (@FILES) {
	my @streams = capturex qw/ tshark -T fields -e tcp.stream -r /, $file;
	chomp(@streams);
	@streams = sort {$a<=>$b} grep {length} keys %{{map {$_=>1} @streams}};
	for my $stream (@streams) {
		next if length $STREAM && $STREAM!=$stream;
		my @basecmd = ('tshark', '-r', $file, '-qz');
		if ($ASCOUT) {
			print "##### "x4, "$file TCP Stream $stream", " #####"x4;
			systemx @basecmd, "follow,tcp,hex,$stream";
		}
		else {
			for (capturex @basecmd, "follow,tcp,raw,$stream") {
				next if !/\S/ || /^=+$/ || /^Filter: tcp.stream eq $stream$/
					|| /^Follow: tcp,raw$/ || /^Node \d+: .+$/;
				my ($indent,$data) = /^(\s*)([0-9a-fA-F]+)$/
					or die "Unknown line format: $_";
				next if $SHOWONLY eq 'left'  &&  length $indent
				     || $SHOWONLY eq 'right' && !length $indent;
				print pack 'H*', $data;
			}
		}
	}
}
close select if defined $OUTFILE;

__END__

=head1 AUTHOR, COPYRIGHT, AND LICENSE

Copyright (c) 2017 Hauke Daempfling (haukex@zero-g.net)
at the Leibniz Institute of Freshwater Ecology and Inland Fisheries (IGB),
Berlin, Germany, L<http://www.igb-berlin.de/>

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program. If not, see L<http://www.gnu.org/licenses/>.

=cut

